(*
 * Copyright (c) 2014 Thomas Gazagnaire <thomas@gazagnaire.org>
 *
 * Permission to use, copy, modify, and distribute this software for any
 * purpose with or without fee is hereby granted, provided that the above
 * copyright notice and this permission notice appear in all copies.
 *
 * THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
 * WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
 * MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
 * ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
 * WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
 * ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
 * OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
 *)

open Project
open Printf

let (/) x y = Filename.concat x y

let (//) x y =
  match x with
  | None   -> y
  | Some x -> Filename.concat x y

let conmap f l = List.concat (List.map f l)

module Variable = struct

  type assign = string

  type t = {
    name    : string;
    assign  : assign;
    contents: contents;
  }

  and contents =
    [ `String of string
    | `Strings of string list
    | `Case of (t list * contents) list ]

  let (=:=) name contents =
    { name; contents; assign = ":=" }

  let (=+=) name contents =
    { name; contents; assign = "+=" }

  let (=?=) name contents =
    { name; contents; assign = ":=" }

  let subst t name ~input ~output =
    { name; assign = "=";
      contents = `String (sprintf "$(${%s}:%s=%s)" t.name input output)
    }

  let name t =
    sprintf "$(%s)" t.name

  let generate buf t =
    let string c =
      bprintf buf "%-15s %s %s\n" t.name t.assign c in
    let rec contents = function
      | `String s   -> string s
      | `Strings ss -> string (String.concat " " ss)
      | `Case cases ->
        let rec aux = function
          | []                -> ()
          | (vars, c) :: rest ->
            if vars <> [] then (
              bprintf buf "ifeq (";
              List.iter (fun var -> bprintf buf "$(%s:1=)" var.name) vars;
              bprintf buf ",)\n";
            );
            contents c;
            if vars <> [] && rest <> [] then
              bprintf buf "else\n";
            aux rest;
            if vars <> [] then
              bprintf buf "endif\n" in
        aux cases in
    contents t.contents

  let shell name command =
    { name; assign = "=";
      contents = `String (sprintf "$(shell %s)" command)
    }

  let files name ~dir ~ext =
    { name; assign = "=";
      contents = `String (sprintf "$(wildcard %s/*.%s)" dir ext) }

  let has_feature f =
    let var = String.uppercase (Feature.name f) in
    for i = 0 to String.length var - 1 do
      match var.[i] with
      | '-' -> var.[i] <- '_'
      | _   -> ()
    done;
    ("HAS_" ^ var) =?= `String (if Feature.default f then "1" else "0")

  let has_native =
    has_feature Feature.native

  let has_native_dynlink =
    has_feature Feature.native_dynlink

end

module Rule = struct

  type t = {
    ext: bool;
    targets: string list;
    prerequisites:string list;
    order_only_prerequisites:string list;
    recipe:string list;
  }

  let create ?(ext=false) ~targets ~prereqs ?(order_only_prereqs=[]) recipe =
    { ext; targets;
      prerequisites=prereqs;
      order_only_prerequisites=order_only_prereqs;
      recipe }

  let generate buf t =
    bprintf buf "%s%s %s%s\n"
      (String.concat " " t.targets)
      (if t.ext then "::" else ":")
      (String.concat " " t.prerequisites)
      (match t.order_only_prerequisites with
       | []  -> ""
       | l   -> sprintf " | %s" (String.concat " " l));
    let () = match t.recipe with
      | [] -> bprintf buf "\t@\n"
      | l  -> List.iter (bprintf buf "\t%s\n") l
    in
    bprintf buf "\n"

  let target = "$@"
  let target_member = "$%"
  let prereq = "$<"
  let changed_prereqs = "$?"
  let prereqs = "$^"
  let dedup_prereqs = "$+"
  let stem = "$*"

end


type t = {
  header: string list;
  phony: string list;
  variables: Variable.t list;
  rules: Rule.t list;
}

let create ?(header=[]) ?(phony=[]) variables rules =
  { phony; header; variables; rules }

let write ?(file="Makefile") t =
  printf "\027[36m+ write %s\027[m\n" file;
  let buf = Buffer.create 1024 in
  bprintf buf "# Generated by ocaml-tools\n\n";
  List.iter (fun s ->
      Buffer.add_string buf s;
      Buffer.add_string buf "\n\n";
    ) t.header;
  let () = match t.phony with
    | [] -> ()
    | l  -> bprintf buf ".PHONY: %s\n\n" (String.concat " " l)
  in
  List.iter (Variable.generate buf) t.variables;
  bprintf buf "\n";
  List.iter (Rule.generate buf) t.rules;
  let oc = open_out file in
  output_string oc (Buffer.contents buf);
  close_out oc

(******************************************************************************)

let buildir = "$(BUILDIR)"

let echo_prereqs =
  sprintf "@echo '\027[36m== Building %s\027[m'" Rule.prereqs

let resolver =
  Ocamlfind.resolver (fun x -> buildir / x)

module rec D: sig
  val variables: Dep.t -> Variable.t list
  val rules    : Dep.t -> Rule.t list
end = struct

  let variables = function
    | `Unit u -> U.variables u
    | `Lib l
    | `Pp l   -> L.variables l
    | _       -> []

  let rules = function
    | `Unit u -> U.rules u
    | `Lib l
    | `Pp l   -> L.rules l
    | _       -> []

end

and U: sig
  val rules      : Unit.t -> Rule.t list
  val variables  : Unit.t -> Variable.t list
  val pp_byte    : Unit.t -> Variable.t option
  val pp_native  : Unit.t -> Variable.t option
  val comp_byte  : Unit.t -> Variable.t
  val comp_native: Unit.t -> Variable.t
end = struct

  let pp_byte t =
    let var = "PP_BYTE" ^ Unit.name t in
    match Flags.pp_byte (Unit.flags t resolver) with
    | [] -> None
    | l  -> Some (Variable.(var =?= `Strings l))

  let pp_native t =
    let var = "PP_NATIVE" ^ Unit.name t in
    match Flags.pp_native (Unit.flags t resolver) with
    | [] -> None
    | l  -> Some (Variable.(var =?= `Strings l))

  let comp_byte t =
    let var = "COMP_BYTE" ^ Unit.name t in
    let flags = Flags.comp_byte (Unit.flags t resolver) in
    Variable.(var =?= `Strings flags)

  let comp_native t =
    let var = "COMP_OPT" ^ Unit.name t in
    let flags = Flags.comp_native (Unit.flags t resolver) in
    Variable.(var =?= `Strings flags)

  let variables t =
    [ comp_byte t;
      comp_native t;
    ] @ (match pp_byte t with
        | None   -> []
        | Some l -> [l]
      ) @ (match pp_native t with
        | None   -> []
        | Some l -> [l])

  let preds t =
    Dep.units (Unit.unpack t) @ Unit.deps t

  (* XXX: handle native pp *)
  let rec rules t =
    let pp = match pp_byte t with
      | None   -> ""
      | Some v -> sprintf "-pp '$(CAMLP4O) %s' " (Variable.name v) in
    let for_pack = match Unit.for_pack t with
      | None   -> ""
      | Some p -> sprintf "-for-pack %s " p in
    let flags = for_pack ^ pp in
    let target ext = Unit.file t resolver ext in
    let source ext = Unit.dir t // Unit.name t ^ ext in

    match Unit.unpack t with
    | [] -> (* Normal compilation unit. *)
      let ln = (* link source file to target directory *)
        let aux ext =
          let source = source ext in
          let target = target ext in
          if Sys.file_exists source then
            [Rule.create[target] [source] [
                (match Unit.build_dir t with
                 | None   -> sprintf "mkdir -p %S" "$(BUILDIR)"
                 | Some d -> sprintf "mkdir -p %s" d);
                sprintf "ln -sf $(shell pwd)/%s %s" source target
              ]]
          else [] in
        aux ".ml" @ aux ".mli" in
      let cmi = (* generate cmis *)
        let targets, prereqs =
          if Sys.file_exists (source ".mli") then [target ".cmi"], [target ".mli"]
          else [target ".cmo"; target ".cmi"], [target ".ml"] in
        [Rule.create targets (prereqs @ Unit.prereqs t resolver`Byte) [
            sprintf "$(OCAMLC) -c %s %s %s"
              flags (Variable.name @@ comp_byte t) Rule.prereq
          ]] in
      let cmo = (* Generate cmos *)
        if Sys.file_exists (source ".mli") then
          [Rule.create [target ".cmo"]
             (target ".ml" :: target ".cmi" :: Unit.prereqs t resolver `Byte)
             [sprintf "$(OCAMLC) -c %s %s %s"
                flags (Variable.name @@ comp_byte t) Rule.prereq]]
        else
          [] in
      let cmx = (* Generate cmxs *)
        [Rule.create [target ".cmx"]
           (target ".ml" :: target ".cmi" :: Unit.prereqs t resolver `Native)
           [sprintf "$(OCAMLOPT) -c %s %s %s"
              flags (Variable.name @@ comp_native t) Rule.prereq]]
      in
      ln @ cmi @ cmo @ cmx

    | units -> (* Packed units *)
      let byte =
        let cmo = List.map (fun u -> Unit.cmo u resolver) units in
        Rule.create [target ".cmo"; target ".cmi"] cmo [
          sprintf "$(OCAMLC) -pack %s %s -o %s" flags Rule.prereq Rule.target_member
        ] in
      let native =
        let cmx = List.map (fun u -> Unit.cmx u resolver) units in
        Rule.create [target ".cmx"] cmx [
          sprintf "$(OCAMLOPT) -pack %s %s -o %s" flags Rule.prereq Rule.target_member
        ] in
      byte :: native :: conmap U.rules units

end

and L: sig
  val rules      : Lib.t -> Rule.t list
  val variables  : Lib.t -> Variable.t list
  val target     : Lib.t -> string
  val link_byte  : Lib.t -> Variable.t
  val link_native: Lib.t -> Variable.t
end = struct

  let target t =
    "lib-" ^ Lib.name t

  let link_byte t =
    let var = "LINK_BYTE_" ^ Lib.name t in
    Variable.(var =?= `Strings (Flags.link_byte @@ Lib.flags t resolver))

  let link_native t =
    let var = "LINK_OPT_" ^ Lib.name t in
    Variable.(var =?= `Strings (Flags.link_native @@ Lib.flags t resolver))

  let variables t =
    let cma  = Lib.cma t resolver in
    let cmxa = Lib.cmxa t resolver in
    let cmxs = Lib.cmxs t resolver in
    let case = [
      [Variable.has_native; Variable.has_native_dynlink], `Strings [cma; cmxa; cmxs];
      [Variable.has_native]                             , `Strings [cma; cmxa];
      []                                                , `Strings [cma];
    ] in
    Variable.(target t =?= `Case case)
    :: link_byte t
    :: link_native t
    :: conmap U.variables (Lib.units t)
    @  conmap D.variables (Lib.deps t |> Dep.closure)

  let rules t =
    let byte =
      Rule.create [Lib.cma t resolver] (Lib.prereqs t resolver `Byte) [
        sprintf "$(OCAMLC) -a %s -o %s" (Variable.name @@ link_byte t) Rule.target
      ] in
    let native mode =
      let file, mode = match mode with
        | `shared  -> Lib.cmxs t resolver, "-shared"
        | `archive -> Lib.cmxa t resolver, "-a" in
      Rule.create [file] (Lib.prereqs t resolver `Native) [
        sprintf "$(OCAMLOPT) %s %s -o %s"
          mode (Variable.name @@ link_native t) Rule.target
      ] in
    Rule.create [target t] [sprintf "$(%s)" (target t)] [echo_prereqs]
    :: byte
    :: native `archive
    :: native `shared
    :: conmap U.rules (Lib.units t)
    @  conmap D.rules (Lib.deps t |> Dep.closure)

end

module B: sig
  val rules      : Bin.t -> Rule.t list
  val variables  : Bin.t -> Variable.t list
  val target     : Bin.t -> string
  val link_byte  : Bin.t -> Variable.t
  val link_native: Bin.t -> Variable.t
end = struct

  let target t =
    "bin-" ^ Bin.name t

  let link_byte t =
    let var = "BIN_BYTE_" ^ Bin.name t in
    Variable.(var =?= `Strings (Flags.link_byte @@ Bin.flags t resolver))

  let link_native t =
    let var = "BIN_OPT_" ^ Bin.name t in
    Variable.(var =?= `Strings (Flags.link_native @@ Bin.flags t resolver))

  let variables t = [
    Variable.(target t =?= `Case [
        [Variable.has_native], `Strings [Bin.byte t resolver; Bin.native t resolver];
        []                   , `String  (Bin.byte t resolver);
      ]);
    link_byte t;
    link_native t;
  ]

  let rules t =
    Rule.create [target t] [sprintf "$(%s)" (target t)] [echo_prereqs]
    ::
    Rule.create [Bin.byte t resolver] (Bin.prereqs t resolver `Byte) [
      sprintf "mkdir -p %s" (buildir / Bin.name t);
      sprintf "$(OCAMLC) %s -o %s" (Variable.name @@ link_byte t) Rule.target;
    ]
    ::
    Rule.create [Bin.native t resolver] (Bin.prereqs t resolver `Native) [
      sprintf "mkdir -p %s" (buildir / Bin.name t);
      sprintf "$(OCAMLOPT) %s -o %s" (Variable.name @@ link_native t) Rule.target;
    ]
    ::
    conmap L.rules (Bin.deps t |> fun d -> Dep.filter_libs d @ Dep.filter_pps d)
    @ conmap U.rules (Dep.filter_units @@ Bin.deps t)

end

(* dedup while keeping the initial order *)
let dedup l =
  let saw = Hashtbl.create (List.length l) in
  let rec aux acc = function
    | []   -> List.rev acc
    | h::t ->
      if Hashtbl.mem saw h then aux acc t
      else (
        Hashtbl.add saw h true;
        aux (h :: acc) t
      ) in
  aux [] l

let of_project ?(buildir="_build") t =
  let libs = Project.libs t in
  let pps = Project.pps t in
  let bins = Project.bins t in
  let features = List.map Variable.has_feature (Project.features t) in
  let variables =
    dedup (
      Variable.(   "BUILDIR"    =?= `String buildir)
      :: Variable.("OCAMLOPT"   =?= `String "ocamlopt")
      :: Variable.("OCAMLC"     =?= `String "ocamlc")
      :: Variable.("CAMLP4O"    =?= `String "camlp4o")
      :: features
      @  conmap L.variables libs
      @  conmap L.variables pps
      @  conmap B.variables bins
    ) in
  let rules =
    dedup (
      conmap L.rules libs
      @ conmap L.rules pps
      @ conmap B.rules bins
    ) in
  let main = Rule.create ~ext:true ~targets:["all"] ~prereqs:[] [
      sprintf "@echo '\027[32m== %s\027[m'"
        (String.concat " " (List.map (fun v ->
             sprintf "%s=%s" v.Variable.name (Variable.name v)
           ) features));
      sprintf "@$(MAKE) %s"
        (String.concat " " (List.map L.target libs @ List.map B.target bins));
      sprintf "@echo '\027[32m== Done!\027[m'";
    ] in
  let clean = Rule.create ~ext:true ~targets:["clean"] ~prereqs:[] [
      "rm -f *~ **/*~";
      sprintf "rm -rf $(BUILDIR)";
    ] in
  let install = Rule.create ~ext:true ~targets:["install"] ~prereqs:["all"] [
      sprintf "@opam-installer --prefix $(shell opam config var prefix) %s.install"
        (Project.name t)
    ] in
  create
    ~phony:["all"; "clean"]
    variables
    (main :: clean :: install :: rules)
